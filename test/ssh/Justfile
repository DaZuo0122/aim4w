default:
    @just --list --unsorted

tool := "my-openssh-server"
docker_image_version := "0.0.1"
docker_image := tool + ":" + docker_image_version
docker_compose_file_keys := "docker-compose.yaml.with_keys"
docker_compose_file_user_pass := "docker-compose.yaml.with_user_pass"

docker_compose_file := docker_compose_file_user_pass

build:
    docker build --no-cache -t {{docker_image}} .

setup:
    @chmod 400 keys/*
    @[ $(dpkg --list | grep sshpass  | wc -l) -gt 0 ] && true || sudo apt install sshpass

start_ssh_server +docker_compose_args="": setup
    #!/bin/bash
    docker-compose --file {{ docker_compose_file }} up {{docker_compose_args}} >/dev/null 2>&1
    max_seconds=30
    for i in 1..$max_seconds; do
        if [ -z `docker ps -q --no-trunc | grep $(docker-compose --file {{ docker_compose_file }} ps -q {{tool}})` ]; then
          echo waiting for my-open-ssh-server..
          sleep 1
        else
          break
        fi
    done

stop:
    @docker-compose --file {{ docker_compose_file }} down >/dev/null 2>&1

test: (start_ssh_server "-d") && stop
    #!/bin/bash
    source ../common.sh
    highlight "\nRunning tests in $PWD\n\n"
    for test in $(grep ^_test_ Justfile | cut -d':' -f1); do
        highlight "$test "
        just $test && true || err "Stopping."
    done

_test_ssh_server_up:
    #!/bin/bash
    source ../common.sh
    user="user"
    pass="pass"
    sshpass -p $pass ssh -q -i keys/id_ed25519 -o "UserKnownHostsFile=/dev/null" -o "StrictHostKeyChecking=no" $user@127.0.0.1 -p 2222 'exit'
    [ $? -eq 0 ] && ok || err "Couldn't connect to local dockerized ssh server."

_test_aim_get:
    #!/bin/bash
    test=$(basename $0) && source ../common.sh
    echo foobar > /tmp/binfile
    sha_input=$(sha256sum /tmp/binfile | cut -d' ' -f1)
    aim ssh://user:pass@127.0.0.1:2222/tmp/binfile $test
    sha_output=$(sha256sum $test | cut -d' ' -f1)
    [ "$sha_input" = "$sha_output" ] && ok || err "ERROR: input and output SHA256s don't match."
    rm $test
